apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = "0.0.1"
group = "com.github.artfable.gradle"

buildscript {
    ext {
        kotlin_version = '1.0.3'
        bintray_version = '1.7.1'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_version"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    compile localGroovy()
    compile gradleApi()
}

repositories {
    jcenter()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

//jar.dependsOn sourcesJar

def pomConfig = {
//    licenses {
//        license {
//            name "MIT"
//            url "https://opensource.org/licenses/MIT"
//            distribution "repo"
//        }
//    }
    developers {
        developer {
            id "artfable"
            name "Artem Veselov"
            email "art-fable@mail.ru"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A gradle plugin for set values to resources')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.ext.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.ext.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        name = 'gradle-resource-plugin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/artfable/gradle-resource-plugin.git'
        version {
            attributes = ['gradle-plugin': 'artfable.resource:com.github.artfable.gradle:gradle-resource-plugin']
        }
    }
}